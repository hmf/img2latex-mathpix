plugins {
    id "java"
    id "application"
    id "org.beryx.runtime" version "1.8.0"
    id 'edu.sc.seis.launch4j' version '2.4.6'
    id "org.openjfx.javafxplugin" version "0.0.8"
}

group "blaise.img2latex"
version "0.7.2"

compileJava {
    sourceCompatibility = 11
    targetCompatibility = 11
}

repositories {
    mavenCentral()
}

javafx {
    version = "${compileJava.targetCompatibility}"
}

application {
    mainClassName = "entry.Main"
    applicationName = "Image2LaTeX"
}

task("addVersion") {
    doLast {
        file("src/main/resources/project.properties").write("applicationName=$applicationName\nversion=$version\n")
    }
}

jar.dependsOn(addVersion)

import org.gradle.internal.os.OperatingSystem
import org.gradle.internal.jvm.Jvm

String osName = OperatingSystem.current().getName()
String osVersion = OperatingSystem.current().getVersion()
//println "*** $osName $osVersion was detected."

dependencies {
    implementation "com.google.code.gson:gson:2.8.6"
    implementation "org.scilab.forge:jlatexmath:1.0.7"
    if (OperatingSystem.current().isWindows()) {
        implementation "org.openjfx:javafx-base:${javafx.version}:win"
        implementation "org.openjfx:javafx-swing:${javafx.version}:win"
        implementation "org.openjfx:javafx-graphics:${javafx.version}:win"
        implementation "org.openjfx:javafx-controls:${javafx.version}:win"
    } else if (OperatingSystem.current().isLinux()) {
        implementation "org.openjfx:javafx-base:${javafx.version}:linux"
        implementation "org.openjfx:javafx-swing:${javafx.version}:linux"
        implementation "org.openjfx:javafx-graphics:${javafx.version}:linux"
        implementation "org.openjfx:javafx-controls:${javafx.version}:linux"
    } else if (OperatingSystem.current().isUnix()) {
        implementation "org.openjfx:javafx-base:${javafx.version}:unix"
        implementation "org.openjfx:javafx-swing:${javafx.version}:unix"
        implementation "org.openjfx:javafx-graphics:${javafx.version}:unix"
        implementation "org.openjfx:javafx-controls:${javafx.version}:unix"
    } else if (OperatingSystem.current().isMacOsX()) {
        implementation "org.openjfx:javafx-base:${javafx.version}:mac"
        implementation "org.openjfx:javafx-swing:${javafx.version}:mac"
        implementation "org.openjfx:javafx-graphics:${javafx.version}:mac"
        implementation "org.openjfx:javafx-controls:${javafx.version}:mac"
    } else {
        throw new  GradleException("Unnown OS: ${osName}:${osVersion}")
    }
}

runtime {
    imageZip = file("${buildDir}/../releases/Image2LaTeX-${version}.zip")
    addOptions("--strip-debug", "--compress", "2", "--no-header-files", "--no-man-pages")
    addModules("java.base", "java.datatransfer", "java.desktop", "java.logging", "java.net.http", "java.prefs", "java.sql", "java.transaction.xa", "jdk.unsupported", "jdk.unsupported.desktop", "java.xml")
    if (OperatingSystem.current().isLinux()) {
        //targetPlatform("linux", "jdk/linux-jdk")
        targetPlatform("linux", Jvm.current().getJavaHome().getAbsolutePath())
    } else if (OperatingSystem.current().isMacOsX()) {
        targetPlatform("macos", "jdk/osx-jdk.jdk/Contents/Home")
    } else if (OperatingSystem.current().isWindows()) {
        targetPlatform("windows", "jdk/windows-jdk")
    } else {
        throw new  GradleException("Unknown OS: ${osName}:${osName}")
    }
}

launch4j {
    mainClassName = "entry.Main"
    bundledJrePath = "jre"
    bundledJre64Bit = true
    version = project.version
    textVersion = project.version
    copyright = "github/blaisewang/img2latex-mathpix"
    icon = "${projectDir}/src/main/resources/windows.ico"
    outputDir = "../releases/${applicationName}-${project.version}-windows"
}
